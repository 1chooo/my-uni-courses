      SUBROUTINE FOURT(DATA,NN,NDIM,ISIGN,IFORM,WORK)

! C     HE COOLEY-TUKEY FAST FOURIER TRANSFORM IN USASI BASIC FORTRAN
! C
! C     TRANSFORM(J1,J2,,,,) = SUM(DATA(I1,I2,,,,)*W1**((I2-1)*(J2-1))
! C                                 *W2**((I2-1)*(J2-1))*,,,),
! C     WHERE I1 AND J1 RUN FROM 1 TO NN(1) AND W1=EXP(ISIGN*2*PI=
! C     SQRT(-1)/NN(1)), ETC.  THERE IS NO LIMIT ON THE DIMENSIONALITY
! C     (NUMBER OF SUBSCRIPTS) OF THE DATA ARRAY.  IF AN INVERSE
! C     TRANSFORM (ISIGN=+1) IS PERFORMED UPON AN ARRAY OF TRANSFORMED
! C     (ISIGN=-1) DATA, THE ORIGINAL DATA WILL REAPPEAR.
! C     MULTIPLIED BY NN(1)*NN(2)*,,,  THE ARRAY OF INPUT DATA MUST BE
! C     IN COMPLEX FORMAT.  HOWEVER, IF ALL IMAGINARY PARTS ARE ZERO (I.E.
! C     THE DATA ARE DISGUISED REAL) RUNNING TIME IS CUT UP TO FORTY PER-
! C     CENT.  (FOR FASTEST TRANSFORM OF REAL DATA, NN(1) SHOULD BE EVEN.)
! C     THE TRANSFORM VALUES ARE ALWAYS COMPLEX AND ARE RETURNED IN THE
! C     ORIGINAL ARRAY OF DATA, REPLACING THE INPUT DATA.  THE LENGTH
! C     OF EACH DIMENSION OF THE DATA ARRAY MAY BE ANY INTEGER.  THE
! C     PROGRAM RUNS FASTER ON COMPOSITE INTEGERS THAN ON PRIMES, AND IS
! C     PARTICULARLY FAST ON NUMBERS RICH IN FACTORS OF TWO.
! C
! C     TIMING IS IN FACT GIVEN BY THE FOLLOWING FORMULA.  LET NTOT BE THE
! C     TOTAL NUMBER OF POINTS (REAL OR COMPLEX) IN THE DATA ARRAY, THAT
! C     IS, NTOT=NN(1)*NN(2)*...  DECOMPOSE NTOT INTO ITS PRIME FACTORS,
! C     SUCH AS 2**K2 * 3**K3 * 5**K5 * ...  LET SUM2 BE THE SUM OF ALL
! C     THE FACTORS OF TWO IN NTOT, THAT IS, SUM2 = 2*K2.  LET SUMF BE
! C     THE SUM OF ALL OTHER FACTORS OF NTOT, THAT IS, SUMF = 3*K3*5*K5*..
! C     THE TIME TAKEN BY A MULTIDIMENSIONAL TRANSFORM ON THESE NTOT DATA
! C     IS T = T0 + NTOT*(T1+T2*SUM2+T3*SUMF).  ON THE CDC 3300 (FLOATING
! C     POINT ADD TIME = SIX MICROSECONDS), T = 3000 + NTOT*(600+40*SUM2+
! C     175*SUMF) MICROSECONDS ON COMPLEX DATA.
! C
! C     IMPLEMENTATION OF THE DEFINITION BY SUMMATION WILL RUN IN A TIME
! C     PROPORTIONAL TO NTOT*(NN(1)+NN(2)+...).  FOR HIGHLY COMPOSITE NTOT
! C     THE SAVINGS OFFERED BY THIS PROGRAM CAN BE DRAMATIC.  A ONE-DIMEN-
! C     SIONAL ARRAY 4000 IN LENGTH WILL BE TRANSFORMED IN 4000*(600+
! C     40*(2+2+2+2+2)+175*(5+5+5)) = 14.5 SECONDS VERSUS ABOUT 4000*
! C     4000*175 = 2800 SECONDS FOR THE STRAIGHTFORWARD TECHNIQUE.
! C
! C     THE FAST FOURIER TRANSFORM PLACES THREE RESTRICTIONS UPON THE
! C     DATA.
! C     1.  THE NUMBER OF INPUT DATA AND THE NUMBER OF TRANSFORM VALUES
! C     MUST BE THE SAME.
! C     2.  BOTH THE INPUT DATA AND THE TRANSFORM VALUES MUST REPRESENT
! C     EQUISPACED POINTS IN THEIR RESPECTIVE DOMAINS OF TIME AND
! C     FREQUENCY.  CALLING THESE SPACINGS DELTAT AND DELTAF, IT MUST BE
! C     TRUE THAT DELTAF=2*PI/(NN(I)*DELTAT).  OF COURSE, DELTAT NEED NOT
! C     BE THE SAME FOR EVERY DIMENSION.
! C     3.  CONCEPTUALLY AT LEAST, THE INPUT DATA AND THE TRANSFORM OUTPUT
! C     REPRESENT SINGLE CYCLES OF PERIODIC FUNCTIONS.
! C
! C     THE CALLING SEQUENCE IS--
! C     CALL FOURT(DATA,NN,NDIM,ISIGN,IFORM,WORK)
! C
! C     DATA IS THE ARRAY USED TO HOLD THE REAL AND IMAGINARY PARTS
! C     OF THE DATA ON INPUT AND THE TRANSFORM VALUES ON OUTPUT.  IT
! C     IS A MULTIDIMENSIONAL FLOATING POINT ARRAY, WITH THE REAL AND
! C     IMAGINARY PARTS OF A DATUM STORED IMMEDIATELY ADJACENT IN STORAGE
! C     (SUCH AS FORTRAN IV PLACES THEM).  NORMAL FORTRAN ORDERING IS
! C     EXPECTED, THE FIRST SUBSCRIPT CHANGING FASTEST.  THE DIMENSIONS
! C     ARE GIVEN IN THE INTEGER ARRAY NN, OF LENGTH NDIM.  ISIGN IS -1
! C     TO INDICATE A FORWARD TRANSFORM (EXPONENTIAL SIGN IS -) AND +1
! C     FOR AN INVERSE TRANSFORM (SIGN IS +).  IFORM IS +1 IF THE DATA ARE
! C     COMPLEX, 0 IF THE DATA ARE REAL.  IF IT IS 0, THE IMAGINARY
! C     PARTS OF THE DATA MUST BE SET TO ZERO.  AS EXPLAINED ABOVE, THE
! C     TRANSFORM VALUES ARE ALWAYS COMPLEX AND ARE STORED IN ARRAY DATA.
! C     WORK IS AN ARRAY USED FOR WORKING STORAGE.  IT IS FLOATING POINT
! C     REAL, ONE DIMENSIONAL OF LENGTH EQUAL TO TWICE THE LARGEST ARRAY
! C     DIMENSION NN(I) THAT IS NOT A POWER OF TWO.  IF ALL NN(I) ARE
! C     POWERS OF TWO, IT IS NOT NEEDED AND MAY BE REPLACED BY ZERO IN THE
! C     CALLING SEQUENCE.  THUS, FOR A ONE-DIMENSIONAL ARRAY, NN(1) ODD,
! C     WORK OCCUPIES AS MANY STORAGE LOCATIONS AS DATA.  IF SUPPLIED,
! C     WORK MUST NOT BE THE SAME ARRAY AS DATA.  ALL SUBSCRIPTS OF ALL
! C     ARRAYS BEGIN AT ONE.
! C
! C     EXAMPLE 1.  THREE-DIMENSIONAL FORWARD FOURIER TRANSFORM OF A
! C     COMPLEX ARRAY DIMENSIONED 32 BY 25 BY 13 IN FORTRAN IV.
! C     DIMENSION DATA(32,25,13),WORK(50),NN(3)
! C     COMPLEX DATA
! C     DATA NN/32,25,13/
! C     DO 1 I=1,32
! C     DO 1 J=1,25
! C     DO 1 K=1,13
! C  1  DATA(I,J,K)=COMPLEX VALUE
! C     CALL FOURT(DATA,NN,3,-1,1,WORK)
! C
! C     EXAMPLE 2.  ONE-DIMENSIONAL FORWARD TRANSFORM OF A REAL ARRAY OF
! C     LENGTH 64 IN FORTRAN II,
! C     DIMENSION DATA(2,64)
! C     DO 2 I=1,64
! C     DATA(1,I)=REAL PART
! C  2  DATA(2,I)=0.
! C     CALL FOURT(DATA,64,1,-1,0,0)
! C
! C     THERE ARE NO ERROR MESSAGES OR ERROR HALTS IN THIS PROGRAM.  THE
! C     PROGRAM RETURNS IMMEDIATELY IF NDIM OR ANY NN(I) IS LESS THAN ONE.
! C
! C     PROGRAM BY NORMAN BRENNER FROM THE BASIC PROGRAM BY CHARLES
! C     RADER,  JUNE 1967.  THE IDEA FOR THE DIGIT REVERSAL WAS
! C     SUGGESTED BY RALPH ALTER.
! C
! C     THIS IS THE FASTEST AND MOST VERSATILE VERSION OF THE FFT KNOWN
! C     TO THE AUTHOR.  A PROGRAM CALLED FOUR2 IS AVAILABLE THAT ALSO
! C     PERFORMS THE FAST FOURIER TRANSFORM AND IS WRITTEN IN USASI BASIC
! C     FORTRAN.  IT IS ABOUT ONE THIRD AS LONG AND RESTRICTS THE
! C     DIMENSIONS OF THE INPUT ARRAY (WHICH MUST BE COMPLEX) TO BE POWERS
! C     OF TWO.  ANOTHER PROGRAM, CALLED FOUR1, IS ONE TENTH AS LONG AND
! C     RUNS TWO THIRDS AS FAST ON A ONE-DIMENSIONAL COMPLEX ARRAY WHOSE
! C     LENGTH IS A POWER OF TWO.
! C
! C     REFERENCE--
! C     IEEE AUDIO TRANSACTIONS (JUNE 1967), SPECIAL ISSUE ON THE FFT.
      DIMENSION DATA(1),NN(1),IFACT(32),WORK(1)
      DATA TWOPI/6.2831853071796/,RTHLF/0.70710678118655/
      DATA NPREV/0/,NP0/0/
! C THE FOLLOWING CALL IS FOR GATHERING STATISTICS ON LIBRARY USE AT NCAR
! C     CALL Q8QST4( 4HXLIB      , 5HFOURT     ,5HFOURT  ,10HVERSION  9)
      IF(NDIM-1)920,1,1
1     NTOT=2
      DO 2 IDIM=1,NDIM
      IF(NN(IDIM))920,920,2
2     NTOT=NTOT*NN(IDIM)
! C
! C     MAIN LOOP FOR EACH DIMENSION
! C
      NP1=2
      DO 910 IDIM=1,NDIM
      N=NN(IDIM)
      NP2=NP1*N
      IF(N-1)920,900,5
! C
! C     IS N A POWER OF TWO AND IF NOT, WHAT ARE ITS FACTORS
! C
5     M=N
      NTWO=NP1
      IF=1
      IDIV=2
10    IQUOT=M/IDIV
      IREM=M-IDIV*IQUOT
      IF(IQUOT-IDIV)50,11,11
11    IF(IREM)20,12,20
12    NTWO=NTWO+NTWO
      IFACT(IF)=IDIV
      IF=IF+1
      M=IQUOT
      GO TO 10
20    IDIV=3
      INON2=IF
30    IQUOT=M/IDIV
      IREM=M-IDIV*IQUOT
      IF(IQUOT-IDIV)60,31,31
31    IF(IREM)40,32,40
32    IFACT(IF)=IDIV
      IF=IF+1
      M=IQUOT
      GO TO 30
40    IDIV=IDIV+2
      GO TO 30
50    INON2=IF
      IF(IREM)60,51,60
51    NTWO=NTWO+NTWO
      GO TO 70
60    IFACT(IF)=M
! C
! C     SEPARATE FOUR CASES--
! C        1. COMPLEX TRANSFORM OR REAL TRANSFORM FOR THE 4TH, 9TH,ETC.
! C           DIMENSIONS.
! C        2. REAL TRANSFORM FOR THE 2ND OR 3RD DIMENSION.  METHOD--
! C           TRANSFORM HALF THE DATA, SUPPLYING THE OTHER HALF BY CON-
! C           JUGATE SYMMETRY.
! C        3. REAL TRANSFORM FOR THE 1ST DIMENSION, N ODD.  METHOD--
! C           SET THE IMAGINARY PARTS TO ZERO.
! C        4. REAL TRANSFORM FOR THE 1ST DIMENSION, N EVEN.  METHOD--
! C           TRANSFORM A COMPLEX ARRAY OF LENGTH N/2 WHOSE REAL PARTS
! C           ARE THE EVEN NUMBERED REAL VALUES AND WHOSE IMAGINARY PARTS
! C           ARE THE ODD NUMBERED REAL VALUES.  SEPARATE AND SUPPLY
! C           THE SECOND HALF BY CONJUGATE SYMMETRY.
! C
70    ICASE=1
      IFMIN=1
      I1RNG=NP1
      IF(IDIM-4)71,100,100
71    IF(IFORM)72,72,100
72    ICASE=2
      I1RNG=NP0*(1+NPREV/2)
      IF(IDIM-1)73,73,100
73    ICASE=3
      I1RNG=NP1
      IF(NTWO-NP1)100,100,74
74    ICASE=4
      IFMIN=2
      NTWO=NTWO/2
      N=N/2
      NP2=NP2/2
      NTOT=NTOT/2
      I=1
      DO 80 J=1,NTOT
      DATA(J)=DATA(I)
80    I=I+2
! C
! C     SHUFFLE DATA BY BIT REVERSAL, SINCE N=2**K.  AS THE SHUFFLING
! C     CAN BE DONE BY SIMPLE INTERCHANGE, NO WORKING ARRAY IS NEEDED
! C
100   IF(NTWO-NP2)200,110,110
110   NP2HF=NP2/2
      J=1
      DO 150 I2=1,NP2,NP1
      IF(J-I2)120,130,130
120   I1MAX=I2+NP1-2
      DO 125 I1=I2,I1MAX,2
      DO 125 I3=I1,NTOT,NP2
      J3=J+I3-I2
      TEMPR=DATA(I3)
      TEMPI=DATA(I3+1)
      DATA(I3)=DATA(J3)
      DATA(I3+1)=DATA(J3+1)
      DATA(J3)=TEMPR
125   DATA(J3+1)=TEMPI
130   M=NP2HF
140   IF(J-M)150,150,145
145   J=J-M
      M=M/2
      IF(M-NP1)150,140,140
150   J=J+M
      GO TO 300
! C
! C     SHUFFLE DATA BY DIGIT REVERSAL FOR GENERAL N
! C
200   NWORK=2*N
      DO 270 I1=1,NP1,2
      DO 270 I3=I1,NTOT,NP2
      J=I3
      DO 260 I=1,NWORK,2
      IF(ICASE-3)210,220,210
210   WORK(I)=DATA(J)
      WORK(I+1)=DATA(J+1)
      GO TO 230
220   WORK(I)=DATA(J)
      WORK(I+1)=0.
230   IFP2=NP2
      IF=IFMIN
240   IFP1=IFP2/IFACT(IF)
      J=J+IFP1
      IF(J-I3-IFP2)260,250,250
250   J=J-IFP2
      IFP2=IFP1
      IF=IF+1
      IF(IFP2-NP1)260,260,240
260   CONTINUE
      I2MAX=I3+NP2-NP1
      I=1
      DO 270 I2=I3,I2MAX,NP1
      DATA(I2)=WORK(I)
      DATA(I2+1)=WORK(I+1)
270   I=I+2
! C
! C     MAIN LOOP FOR FACTORS OF TWO.  PERFORM FOURIER TRANSFORMS OF
! C     LENGTH FOUR, WITH ONE OF LENGTH TWO IF NEEDED.  THE TWIDDLE FACTOR
! C     W=EXP(ISIGN*2*PI*SQRT(-1)*M/(4*MMAX)).  CHECK FOR W=ISIGN*SQRT(-1)
! C     AND REPEAT FOR W=W*(1+ISIGN*SQRT(-1))/SQRT(2).
! C
300   IF(NTWO-NP1)600,600,305
305   NP1TW=NP1+NP1
      IPAR=NTWO/NP1
310   IF(IPAR-2)350,330,320
320   IPAR=IPAR/4
      GO TO 310
330   DO 340 I1=1,I1RNG,2
      DO 340 K1=I1,NTOT,NP1TW
      K2=K1+NP1
      TEMPR=DATA(K2)
      TEMPI=DATA(K2+1)
      DATA(K2)=DATA(K1)-TEMPR
      DATA(K2+1)=DATA(K1+1)-TEMPI
      DATA(K1)=DATA(K1)+TEMPR
340   DATA(K1+1)=DATA(K1+1)+TEMPI
350   MMAX=NP1
360   IF(MMAX-NTWO/2)370,600,600
370   LMAX=MAX0(NP1TW,MMAX/2)
      DO 570 L=NP1,LMAX,NP1TW
      M=L
      IF(MMAX-NP1)420,420,380
380   THETA=-TWOPI*FLOAT(L)/FLOAT(4*MMAX)
      IF(ISIGN)400,390,390
390   THETA=-THETA
400   WR=COS(THETA)
      WI=SIN(THETA)
410   W2R=WR*WR-WI*WI
      W2I=2.*WR*WI
      W3R=W2R*WR-W2I*WI
      W3I=W2R*WI+W2I*WR
420   DO 530 I1=1,I1RNG,2
      KMIN=I1+IPAR*M
      IF(MMAX-NP1)430,430,440
430   KMIN=I1
440   KDIF=IPAR*MMAX
450   KSTEP=4*KDIF
      IF(KSTEP-NTWO)460,460,530
460   DO 520 K1=KMIN,NTOT,KSTEP
      K2=K1+KDIF
      K3=K2+KDIF
      K4=K3+KDIF
      IF(MMAX-NP1)470,470,480
470   U1R=DATA(K1)+DATA(K2)
      U1I=DATA(K1+1)+DATA(K2+1)
      U2R=DATA(K3)+DATA(K4)
      U2I=DATA(K3+1)+DATA(K4+1)
      U3R=DATA(K1)-DATA(K2)
      U3I=DATA(K1+1)-DATA(K2+1)
      IF(ISIGN)471,472,472
471   U4R=DATA(K3+1)-DATA(K4+1)
      U4I=DATA(K4)-DATA(K3)
      GO TO 510
472   U4R=DATA(K4+1)-DATA(K3+1)
      U4I=DATA(K3)-DATA(K4)
      GO TO 510
480   T2R=W2R*DATA(K2)-W2I*DATA(K2+1)
      T2I=W2R*DATA(K2+1)+W2I*DATA(K2)
      T3R=WR*DATA(K3)-WI*DATA(K3+1)
      T3I=WR*DATA(K3+1)+WI*DATA(K3)
      T4R=W3R*DATA(K4)-W3I*DATA(K4+1)
      T4I=W3R*DATA(K4+1)+W3I*DATA(K4)
      U1R=DATA(K1)+T2R
      U1I=DATA(K1+1)+T2I
      U2R=T3R+T4R
      U2I=T3I+T4I
      U3R=DATA(K1)-T2R
      U3I=DATA(K1+1)-T2I
      IF(ISIGN)490,500,500
490   U4R=T3I-T4I
      U4I=T4R-T3R
      GO TO 510
500   U4R=T4I-T3I
      U4I=T3R-T4R
510   DATA(K1)=U1R+U2R
      DATA(K1+1)=U1I+U2I
      DATA(K2)=U3R+U4R
      DATA(K2+1)=U3I+U4I
      DATA(K3)=U1R-U2R
      DATA(K3+1)=U1I-U2I
      DATA(K4)=U3R-U4R
520   DATA(K4+1)=U3I-U4I
      KDIF=KSTEP
      KMIN=4*(KMIN-I1)+I1
      GO TO 450
530   CONTINUE
      M=M+LMAX
      IF(M-MMAX)540,540,570
540   IF(ISIGN)550,560,560
550   TEMPR=WR
      WR=(WR+WI)*RTHLF
      WI=(WI-TEMPR)*RTHLF
      GO TO 410
560   TEMPR=WR
      WR=(WR-WI)*RTHLF
      WI=(TEMPR+WI)*RTHLF
      GO TO 410
570   CONTINUE
      IPAR=3-IPAR
      MMAX=MMAX+MMAX
      GO TO 360
! C
! C     MAIN LOOP FOR FACTORS NOT EQUAL TO TWO.  APPLY THE TWIDDLE FACTOR
! C     W=EXP(ISIGN*2*PI*SQRT(-1)*(J1-1)*(J2-J1)/(IFP1+IFP2)), THEN
! C     PERFORM A FOURIER TRANSFORM OF LENGTH IFACT(IF), MAKING USE OF
! C     CONJUGATE SYMMETRIES.
! C
600   IF(NTWO-NP2)605,700,700
605   IFP1=NTWO
      IF=INON2
      NP1HF=NP1/2
610   IFP2=IFACT(IF)*IFP1
      J1MIN=NP1+1
      IF(J1MIN-IFP1)615,615,640
615   DO 635 J1=J1MIN,IFP1,NP1
      THETA=-TWOPI*FLOAT(J1-1)/FLOAT(IFP2)
      IF(ISIGN)625,620,620
620   THETA=-THETA
625   WSTPR=COS(THETA)
      WSTPI=SIN(THETA)
      WR=WSTPR
      WI=WSTPI
      J2MIN=J1+IFP1
      J2MAX=J1+IFP2-IFP1
      DO 635 J2=J2MIN,J2MAX,IFP1
      I1MAX=J2+I1RNG-2
      DO 630 I1=J2,I1MAX,2
      DO 630 J3=I1,NTOT,IFP2
      TEMPR=DATA(J3)
      DATA(J3)=DATA(J3)*WR-DATA(J3+1)*WI
630   DATA(J3+1)=TEMPR*WI+DATA(J3+1)*WR
      TEMPR=WR
      WR=WR*WSTPR-WI*WSTPI
635   WI=TEMPR*WSTPI+WI*WSTPR
640   THETA=-TWOPI/FLOAT(IFACT(IF))
      IF(ISIGN)650,645,645
645   THETA=-THETA
650   WSTPR=COS(THETA)
      WSTPI=SIN(THETA)
      J2RNG=IFP1*(1+IFACT(IF)/2)
      DO 695 I1=1,I1RNG,2
      DO 695 I3=I1,NTOT,NP2
      J2MAX=I3+J2RNG-IFP1
      DO 690 J2=I3,J2MAX,IFP1
      J1MAX=J2+IFP1-NP1
      DO 680 J1=J2,J1MAX,NP1
      J3MAX=J1+NP2-IFP2
      DO 680 J3=J1,J3MAX,IFP2
      JMIN=J3-J2+I3
      JMAX=JMIN+IFP2-IFP1
      I=1+(J3-I3)/NP1HF
      IF(J2-I3)655,655,665
655   SUMR=0.
      SUMI=0.
      DO 660 J=JMIN,JMAX,IFP1
659   SUMR=SUMR+DATA(J)
660   SUMI=SUMI+DATA(J+1)
      WORK(I)=SUMR
      WORK(I+1)=SUMI
      GO TO 680
665   ICONJ=1+(IFP2-2*J2+I3+J3)/NP1HF
      J=JMAX
      SUMR=DATA(J)
      SUMI=DATA(J+1)
      OLDSR=0.
      OLDSI=0.
      J=J-IFP1
670   TEMPR=SUMR
      TEMPI=SUMI
      SUMR=TWOWR*SUMR-OLDSR+DATA(J)
      SUMI=TWOWR*SUMI-OLDSI+DATA(J+1)
      OLDSR=TEMPR
      OLDSI=TEMPI
      J=J-IFP1
      IF(J-JMIN)675,675,670
675   TEMPR=WR*SUMR-OLDSR+DATA(J)
      TEMPI=WI*SUMI
      WORK(I)=TEMPR-TEMPI
      WORK(ICONJ)=TEMPR+TEMPI
      TEMPR=WR*SUMI-OLDSI+DATA(J+1)
      TEMPI=WI*SUMR
      WORK(I+1)=TEMPR+TEMPI
      WORK(ICONJ+1)=TEMPR-TEMPI
680   CONTINUE
      IF(J2-I3)685,685,686
685   WR=WSTPR
      WI=WSTPI
      GO TO 690
686   TEMPR=WR
      WR=WR*WSTPR-WI*WSTPI
      WI=TEMPR*WSTPI+WI*WSTPR
690   TWOWR=WR+WR
      I=1
      I2MAX=I3+NP2-NP1
      DO 695 I2=I3,I2MAX,NP1
      DATA(I2)=WORK(I)
      DATA(I2+1)=WORK(I+1)
695   I=I+2
      IF=IF+1
      IFP1=IFP2
      IF(IFP1-NP2)610,700,700
! C
! C     COMPLETE A REAL TRANSFORM IN THE 1ST DIMENSION, N EVEN, BY CON-
! C     JUGATE SYMMETRIES.
! C
700   GO TO (900,800,900,701),ICASE
701   NHALF=N
      N=N+N
      THETA=-TWOPI/FLOAT(N)
      IF(ISIGN)703,702,702
702   THETA=-THETA
703   WSTPR=COS(THETA)
      WSTPI=SIN(THETA)
      WR=WSTPR
      WI=WSTPI
      IMIN=3
      JMIN=2*NHALF-1
      GO TO 725
710   J=JMIN
      DO 720 I=IMIN,NTOT,NP2
      SUMR=(DATA(I)+DATA(J))/2.
      SUMI=(DATA(I+1)+DATA(J+1))/2.
      DIFR=(DATA(I)-DATA(J))/2.
      DIFI=(DATA(I+1)-DATA(J+1))/2.
      TEMPR=WR*SUMI+WI*DIFR
      TEMPI=WI*SUMI-WR*DIFR
      DATA(I)=SUMR+TEMPR
      DATA(I+1)=DIFI+TEMPI
      DATA(J)=SUMR-TEMPR
      DATA(J+1)=-DIFI+TEMPI
720   J=J+NP2
      IMIN=IMIN+2
      JMIN=JMIN-2
      TEMPR=WR
      WR=WR*WSTPR-WI*WSTPI
      WI=TEMPR*WSTPI+WI*WSTPR
725   IF(IMIN-JMIN)710,730,740
730   IF(ISIGN)731,740,740
731   DO 735 I=IMIN,NTOT,NP2
735   DATA(I+1)=-DATA(I+1)
740   NP2=NP2+NP2
      NTOT=NTOT+NTOT
      J=NTOT+1
      IMAX=NTOT/2+1
745   IMIN=IMAX-2*NHALF
      I=IMIN
      GO TO 755
750   DATA(J)=DATA(I)
      DATA(J+1)=-DATA(I+1)
755   I=I+2
      J=J-2
      IF(I-IMAX)750,760,760
760   DATA(J)=DATA(IMIN)-DATA(IMIN+1)
      DATA(J+1)=0.
      IF(I-J)770,780,780
765   DATA(J)=DATA(I)
      DATA(J+1)=DATA(I+1)
770   I=I-2
      J=J-2
      IF(I-IMIN)775,775,765
775   DATA(J)=DATA(IMIN)+DATA(IMIN+1)
      DATA(J+1)=0.
      IMAX=IMIN
      GO TO 745
780   DATA(1)=DATA(1)+DATA(2)
      DATA(2)=0.
      GO TO 900
! C
! C     COMPLETE A REAL TRANSFORM FOR THE 2ND OR 3RD DIMENSION BY
! C     CONJUGATE SYMMETRIES.
! C
800   IF(I1RNG-NP1)805,900,900
805   DO 860 I3=1,NTOT,NP2
      I2MAX=I3+NP2-NP1
      DO 860 I2=I3,I2MAX,NP1
      IMIN=I2+I1RNG
      IMAX=I2+NP1-2
      JMAX=2*I3+NP1-IMIN
      IF(I2-I3)820,820,810
810   JMAX=JMAX+NP2
820   IF(IDIM-2)850,850,830
830   J=JMAX+NP0
      DO 840 I=IMIN,IMAX,2
      DATA(I)=DATA(J)
      DATA(I+1)=-DATA(J+1)
840   J=J-2
850   J=JMAX
      DO 860 I=IMIN,IMAX,NP0
      DATA(I)=DATA(J)
      DATA(I+1)=-DATA(J+1)
860   J=J-NP0
! C
! C     END OF LOOP ON EACH DIMENSION
! C
900   NP0=NP1
      NP1=NP2
910   NPREV=N
920   RETURN
      END
